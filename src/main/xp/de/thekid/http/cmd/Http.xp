/* This class is part of the XP framework
 *
 * $Id$ 
 */

package de.thekid.http.cmd;

import peer.http.HttpConnection;
import peer.http.HttpRequest;
import lang.types.StringExtensions;

/**
 * HTTP Command line utility
 *
 * Usage:
 * <pre>
 *   $ http METHOD URL [data]
 * </pre>
 *
 * @see   https://github.com/jkbr/httpie
 */
public class Http extends util.cmd.Command {
  protected HttpConnection $conn;
  protected [:string] $headers;
  protected [:string] $params;
  protected var $formatter = ParameterFormat::$JSON;
  protected var $request = new HttpRequest();

  /**
   * Sets method to use
   *
   */
  [@arg(position = 0)]
  public void setMethod(string $method) {
    $this.request.setMethod($method.toUpper());
  }
  
  /**
   * Sets URL
   *
   */
  [@arg(position = 1)]
  public void setURL(string $url) {
    if (!$url.contains('://')) {
      $url= 'http://' ~ $url;
    }
    $this.conn= new HttpConnection($url);
    $this.conn.create($this.request);
  }
  
  /**
   * Use JSON
   *
   */
  [@arg(name = 'json', short = 'j')]
  public void useJson() {
    $this.formatter= ParameterFormat::$JSON;
  }
  
  /**
   * Use form parameters
   *
   */
  [@arg(name = 'form', short = 'f')]
  public void useForm() {
    $this.formatter= ParameterFormat::$FORM;
  }
  
  /**
   * Use the rest of the arguments
   * <ul>
   *   <li>Headers: Name:Value</li>
   *   <li>Params:  Name=Value</li>
   * </ul>
   */
  [@args(select = '[2..]')]
  public void setArguments(string[] $arguments) {
    $header= new text.regex.Scanner("%[^:]:%[^\n]");
    $params= new text.regex.Scanner("%[^=]=%[^\n]");
    foreach ($arg in $arguments) {
      if ($arg.contains(':')) {
        $match= $header.match($arg).group(0);
        $this.headers[$match[1]]= $match[2];
      } else if ($arg.contains('=')) {
        $match= $params.match($arg).group(0);
        $this.params[$match[1]]= $match[2];
      }
    }
  }

  /**
   * Entry point
   *
   */
  public int run() {
    if ('GET' === $this.request.method || 'HEAD' === $this.request.method) {
      $this.request.setParameters($this.params);
    } else {
      $this.request.setHeader('Content-Type', $this.formatter.contentType());
      $this.request.setParameters($this.formatter.format($this.params));
    }

    $this.request.addHeaders($this.headers);
    $this.err.writeLine($this.request.getRequestString());
    try {
      $response= $this.conn.send($this.request);
    } catch (io.IOException $e) {
      $this.err.writeLine('*** ', $e);
      return 1;
    }
    $this.err.writeLine($response.getResponseString());
    
    return 0;
  }
}
